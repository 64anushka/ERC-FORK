---
eip: 7586
title: Interest Rate Swaps
description: Defines a specification for interest rate swaps derivative contracts
author: Samuel Gwlanold Edoumou (@Edoumou)
discussions-to: https://ethereum-magicians.org/t/interest-rate-swaps/17777
status: Draft
type: Standards Track
category: ERC
created: 2023-12-31
requires: 20
---

## Abstract

This proposal introduces a standardized framework for on-chain interest rate swaps. The proposed standard aims to facilitate the seamless exchange of fixed and floating interest rate cash flows between parties, providing a foundation for decentralized finance (DeFi) applications. 

## Motivation

Interest Rate Swapping (IRS) denotes a derivative contract wherein two parties mutually consent to exchange a series of forthcoming interest payments based on a specified notional amount. This financial instrument serves as a strategic tool for hedging against interest rate fluctuations. The mechanism entails the utilization of a benchmark index to facilitate the exchange between a variable interest rate and a fixed rate. Despite its widespread use, there is currently an absence of a standardized framework that enables the representation of IRS contracts on blockchain platforms.

The formulation of a standardized protocol is imperative to address this gap. This standard would establish a consistent and transparent methodology for depicting IRS contracts within the blockchain environment. By doing so, it would enhance the interoperability, security, and efficiency of interest rate swap transactions on distributed ledger technology.

## Specification

The key words "MUST", "MUST NOT", "REQUIRED", "SHALL", "SHALL NOT", "SHOULD", "SHOULD NOT", "RECOMMENDED", "NOT RECOMMENDED", "MAY", and "OPTIONAL" in this document are to be interpreted as described in RFC 2119 and RFC 8174.

Every contract compliant with this ERC MUST implement the following Interface. The contract MUST inherit the [ERC-20](./eip-20) to tokenize the swap cash flows.

```solidity
pragma solidity ^0.8.0;

/**
* @title ERC-7586 Interest Rate Swaps
*/
interface IERC7586 /** is ERC20 */ {
    // events
    /**
    * @notice MUST be emitted when interest rates are swapped
    * @param _amount the interest difference to be transferred
    * @param _account the recipient account to send the interest difference to. MUST be either the `payer` or the `receiver`
    */
    event Swap(uint256 _amount, address _account);

    /**
    * @notice MUST be emitted when the swap contract is terminated
    * @param _payer the swap payer
    * @param _receiver the swap receiver
    */
    event TerminateSwap(address indexed _payer, address indexed _receiver);

    // functions
    /**
    *  @notice Returns the IRS payer account address. The party who aggreed to pay fixed interest
    */
    function payer() external view returns(address);

    /**
    *  @notice Returns the IRS receiver account address. The party who aggreed to pay floating interest
    */
    function receiver() external view returns(address);

    /**
    *  @notice Returns the fixed interest rate. It is RECOMMENDED to express the swap rate in basis point unit
    *          1 basis point = 0.01% = 0.0001
    *          ex: if interest rate = 2.5%, then swapRate() => 250 basis points
    */
    function swapRate() external view returns(uint256);

    /**
    *  @notice Returns the floating rate spread, i.e. the fixed part of the floating interest rate. It is RECOMMENDED to express the spread in basis point unit
    *          1 basis point = 0.01% = 0.0001
    *          ex: if spread = 0.5%, then floatingRateSpread() => 50 basis points
    *
    *          floatingRate = benchmark + spread
    */
    function spread() external view returns(uint256);

    /**
    * @notice Returns the contract address of the asset to be transferred when swapping IRS. Depending on what the two parties aggreed upon, this could be a currency, etc.
    *         Example: If the two parties agreed to swap interest rates in USDC, then this function should return the USDC contract address.
    *                  This address SHOULD be used in the `swap` function to transfer the interest difference to either the `payer` or the `receiver`. Example: IERC(assetContract).transfer
    */
    function assetContract() external view returns(address);

    /**
    *  @notice Returns the notional amount in unit of asset to be transferred when swapping IRS. This amount serves as the basis for calculating the interest payments, and may not be exchanged
    *          Example: If the two parties aggreed to swap interest rates in USDC, then the notional amount may be equal to 1,000,000 USDC, etc 
    */
    function notionalAmount() external view returns(uint256);

    /**
    *  @notice Returns the interest payment frequency
    */
    function paymentFrequency() external view returns(uint256);

    /**
    *  @notice Returns an array of specific dates on which the interest payments are exchanged. Each date MUST be a Unix timestamp like the one returned by block.timestamp
    *          The length of the array returned by this function MUST equal the total number of swaps that should be realized
    *
    *  OPTIONAL
    */
    function paymentDates() external view returns(uint256[] memory);

    /**
    *  @notice Returns the starting date of the swap contract. This is a Unix Timestamp like the one returned by block.timestamp
    */
    function startingDate() external view returns(uint256);

    /**
    *  @notice Returns the maturity date of the swap contract. This is a Unix Timestamp like the one returned by block.timestamp
    */
    function maturityDate() external view returns(uint256);

    /**
    *  @notice Returns the benchmark in basis point unit
    *          Example: value of one the following rates: CF BIRC, EURIBOR, HIBOR, SHIBOR, SOFR, SONIA, TONAR, etc.
    */
    function benchmark() external view returns(uint256);

    /**
    *  @notice Returns the oracle contract address for the benchmark rate.
    *          This contract SHOULD be used to fetch real time benchmark rate
    *          Example: Contract address for `CF BIRC`
    *
    *  OPTIONAL. The two parties MAY agree to set the benchmark manually
    */
    function oracleContractForBenchmark() external view returns(address);

    /**
    * @notice Gives agreement to swap IRS. After the contract deployment and prior to the initiation of the first payment, it is imperative for both the `payer` and the `receiver` to invoke this function as an expression of their agreement
    */
    function agree() external;

    /**
    *  @notice Makes swap calculation and transfers the interest difference to either the `payer` or the `receiver`
    */
    function swap() external returns(bool);

    /**
    *  @notice Terminates the swap contract before its maturity date. It is RECOMMENDED to integrate a multisig process to terminate the swap contract.
    *          Both the payer and the receiver MUST sign for the termination to be successful
    */
    function terminateSwap() external;
}
```
### Tokenization of Swap Cash Flows

The interest payments associated with the IRS MUST be tokenized by issuing digital ERC-20 tokens to the respective parties according to the terms of the swap. Each token SHOULD represent a specific interest payment. Everytime a swap happens (the `swap` function is called), one token MUST be burned from each party.

## Rationale

The establishment of [ERC-7586](./eip-7586) for Interest Rate Swaps is rooted in the critical need for a standardized framework within the decentralized finance (DeFi) landscape.

Traditional financial markets have long utilized Interest Rate Swaps (IRS) as a fundamental derivative for managing interest rate risk. The integration of such instruments within blockchain platforms marks a pioneering leap toward the evolution of decentralized financial instruments. ERC-7586 aims to spearhead this evolution by introducing a standardized protocol tailored for on-chain interest rate swaps.

### Bridging the Standardization Gap:

Despite the widespread use of IRS, the absence of a standardized framework for its representation on blockchain platforms has hindered the broader adoption and interoperability of decentralized applications (dApps) within the DeFi ecosystem. ERC-7586 addresses this deficiency by providing a well-defined, transparent, and interoperable standard. This standardization fosters a cohesive environment, enabling seamless integration with existing protocols, platforms, and emerging DeFi innovations.

### Interoperability and Ecosystem Growth:

The standardized protocol presented in ERC-7586 is designed to enhance interoperability across diverse DeFi applications and platforms. By establishing common conventions and methodologies, this proposal encourages the creation of a cohesive ecosystem where different projects can seamlessly interact, fostering collaborative innovation and mutual growth.

### Security and Efficiency Enhancement:

In the absence of a standardized protocol, there is an inherent risk of smart contract vulnerabilities and inconsistencies in the implementation of interest rate swaps on blockchain platforms. ERC-7586 mitigates these risks by providing a clear and consistent set of functions and properties. This not only ensures security but also contributes to the overall efficiency of interest rate swap transactions conducted on distributed ledger technology.

### Future-Proofing the DeFi Landscape:

The formulation of ERC-7586 is forward-thinking, anticipating the evolving needs of the DeFi space. By establishing a standardized framework for interest rate swaps, this proposal sets the stage for the integration of advanced financial instruments and complex derivatives, contributing to the maturation and sustainability of the decentralized financial landscape.

## Backwards Compatibility

As mentioned in the `specification` section, this standard is backward compatible with ERC-20.

## Reference Implementation

The complete reference implementation can be found [here](../assets/eip-7586/ERC7586.sol).

This reference implementation serves as a foundation for the implementation of more advanced types of swaps.

## Security Considerations

Security considerations of various types must be thoroughly evaluated

* Interest Rate Risk: This pertains to the potential impact of fluctuations in interest rates.
* Credit Risk: There exists the possibility that one or both parties may default on their respective responsibilities.
* ERC-20 Risks: All security aspects outlined in the ERC-20 standard must be taken into account.

Both parties must acknowledge their awareness of these security risks before proceeding with the implementation of the standard.

## Copyright

Copyright and related rights waived via [CC0](../LICENSE.md).

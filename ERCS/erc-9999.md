---
eip: 9999
title: Named Token, an Extension of ERC-721
description: Add a unique name to ERC-721 tokens.
author: Chen Liaoyuan (@chenly)
discussions-to: https://ethereum-magicians.org/t/erc-9999-name-token/12345
status: Draft
type: Standards Track
category: ERC
created: 2024-02-27
requires: 721
---

## Abstract

This standard is an extension of [ERC-721](./eip-721.md). It allows each token to have a unique name, enabling direct queries of token ownership by name and permitting owners to set or change the names of their tokens.

## Specification

The key words "MUST", "MUST NOT", "REQUIRED", "SHALL", "SHALL NOT", "SHOULD", "SHOULD NOT", "RECOMMENDED", "NOT RECOMMENDED", "MAY", and "OPTIONAL" in this document are to be interpreted as described in RFC 2119 and RFC 8174.

Implementers of this extension **MUST** have all of the following functions:

```solidity
pragma solidity ^0.8.0;

interface NamedToken /* is IERC721 */ {
    /**
     * @dev Returns the name of the specified token.
     * @param tokenId uint256 ID of the token to query
     * @return name string representing the name of the given token ID
     */
    function name(uint256 tokenId) external view returns (string memory);

    /**
     * @dev Returns the owner of the specified token name.
     * @param name string name of the token to query
     * @return owner address representing the owner of the given token name
     */
    function ownerOf(string memory name) external view returns (address);

    /**
     * @dev Mints a new token with a name to a specified address. This function is typically internal and might not
     * appear in the interface but is described here for completeness.
     * @param to address of the future owner of the token
     * @param tokenId uint256 ID of the token to be minted
     * @param name string name to be assigned to this token
     */
    // function _mint(address to, uint256 tokenId, string memory name) internal;

    /**
     * @dev Sets a name for a given token.
     * @param tokenId uint256 ID of the token to set its name
     * @param name string name to be set to the token
     */
    function setName(uint256 tokenId, string memory name) external;
}
```

## Rationale

Here are a few design decisions and why they were made:

### Intuitive Token IDs

Current token IDs are predominantly numeric, lacking intuitiveness. The extension of the [ERC-721](./eip-721.md) standard to support Named Tokens aims to facilitate the acceptance and use of this standard within the NFT marketplace. By moving beyond mere numbers to include distinctive names, token IDs can more directly reflect the unique identity or value they represent.

### Expanding NFT Use Cases

By allowing each token to possess a unique name, we unlock new application scenarios in ecosystems built around scarce username resources, such as domain name registration systems. In such scenarios, domain owners can demonstrate ownership directly by holding the corresponding token, thereby broadening the application spectrum and enhancing the value of NFTs.

## Backwards Compatibility

This standard is fully [ERC-721](./eip-721.md) compatible.

## Reference Implementation

```solidity
pragma solidity ^0.8.0;

import "@openzeppelin/contracts/token/ERC721/ERC721.sol";
import "@openzeppelin/contracts/utils/Strings.sol";

contract NamedToken is ERC721 {
    using Strings for uint256;

    // Mapping from token ID to name
    mapping(uint256 => string) private _tokenNames;

    // Mapping from name to token ID
    mapping(string => uint256) private _nameToTokenId;

    /**
     * @dev Returns the name of the specified token.
     * @param tokenId uint256 ID of the token to query
     * @return name string representing the name of the given token ID
     */
    function name(uint256 tokenId) public view returns (string memory) {
        require(_exists(tokenId), "ERC721NamedExtension: Name query for nonexistent token");
        return _tokenNames[tokenId];
    }

    /**
     * @dev Returns the owner of the specified token name.
     * @param name string name of the token to query
     * @return owner address representing the owner of the given token name
     */
    function ownerOf(string memory name) public view returns (address) {
        uint256 tokenId = _nameToTokenId[name];
        require(_exists(tokenId), "ERC721NamedExtension: Owner query for nonexistent name");
        return ownerOf(tokenId);
    }

    /**
     * @dev Internal function to mint a new token with a name.
     * @param to address of the future owner of the token
     * @param tokenId uint256 ID of the token to be minted
     * @param name string name to be assigned to this token
     */
    function _mint(address to, uint256 tokenId, string memory name) internal virtual {
        super._mint(to, tokenId); // Call the inherited _mint function
        _setTokenName(tokenId, name);
    }

    /**
     * @dev Public function to set a name for a token.
     * @param tokenId uint256 ID of the token to set its name
     * @param name string name to be set to the token
     */
    function setName(uint256 tokenId, string memory name) public {
        require(_exists(tokenId), "ERC721NamedExtension: Set name for nonexistent token");
        require(ownerOf(tokenId) == msg.sender, "ERC721NamedExtension: Caller is not the token owner");
        
        _setTokenName(tokenId, name);
    }

    /**
     * @dev Internal function to set the token name.
     * @param tokenId uint256 ID of the token
     * @param name string name to be assigned to the token
     */
    function _setTokenName(uint256 tokenId, string memory name) internal {
        _tokenNames[tokenId] = name;
        _nameToTokenId[name] = tokenId;
    }
}
```

## Security Considerations

No security considerations were found.

## Copyright

Copyright and related rights waived via [CC0](../LICENSE.md).
---
eip: 7622
title: Pre-Authorized Service Payment Protocol
description: A protocol for automated, pre-approved ERC-20 deductions by authorized providers.
author: Chen Liaoyuan (@chenly) <cly@kip.pro>
discussions-to: https://ethereum-magicians.org/t/erc-7622-pre-authorized-service-payment-protocol/18586
status: Draft
type: Standards Track
category: ERC
created: 2024-02-10
requires: 20
---

## Abstract

This proposal introduces a protocol aimed at enabling pre-authorization and pre-funded functionality within [ERC-20](./eip-20.md) token payments, facilitating the aggregation of token funds. Users are empowered to pre-authorize specific service providers to deduct funds within predetermined limits, ensuring smooth and secure transactions. Additionally, users retain the flexibility to modify or revoke authorizations for particular service providers. This protocol enhances the efficiency, security, and flexibility of [ERC-20](./eip-20.md) token payments within decentralized applications, while also providing the opportunity for funds deposited in the contract to generate returns.

## Motivation

In certain DApps, there is a need to obtain user fund authorization without relying on the [ERC-20](./eip-20.md) `approve` method. The reluctance stems from the fact that the `approve` method does not involve depositing funds into a trusted third-party entity. To address this concern, there is a proposal to introduce a third-party contract capable of receiving and managing authorized funds. These third-party entities could include reputable organizations such as charities or authoritative institutions. By channeling authorized funds into dedicated pools, these entities could generate charitable returns or facilitate community benefits. Furthermore, there is potential to implement governance mechanisms such as DAO governance or transparent public oversight to manage the list of authorized service providers. This would enhance trust and transparency, allowing users to dynamically manage their authorizations while ensuring the integrity of the service provider ecosystem.

## Specification

The key words "MUST", "MUST NOT", "REQUIRED", "SHALL", "SHALL NOT", "SHOULD", "SHOULD NOT", "RECOMMENDED", "NOT RECOMMENDED", "MAY", and "OPTIONAL" in this document are to be interpreted as described in RFC 2119 and RFC 8174.

Implementers of this protocol **MUST** have all of the following functions:

```solidity
pragma solidity ^0.8.20;

interface IERC7622 {

    /**
     * @dev Event emitted when a service provider is registered.
     * @param serviceProvider The address of the registered service provider.
     */
    event ServiceProviderRegistered(address serviceProvider);

    /**
     * @dev Event emitted when a service provider is deregistered.
     * @param serviceProvider The address of the deregistered service provider.
     */
    event ServiceProviderDeregistered(address serviceProvider);

    /**
     * @dev Event emitted when authorization amount is updated.
     * @param user The address of the user whose authorization is updated.
     * @param serviceProvider The address of the service provider.
     * @param newAuthorizedAmount The new authorized amount.
     */
    event AuthorizationUpdated(address indexed user, address indexed serviceProvider, uint256 newAuthorizedAmount);

    /**
     * @dev Event emitted when funds are deducted.
     * @param user The address of the user from whom funds are deducted.
     * @param serviceProvider The address of the service provider.
     * @param amount The amount of funds deducted.
     * @param _data The data associated with the deduction.
     */
    event FundsDeducted(address indexed user, address indexed serviceProvider, uint256 amount, bytes _data);

    /**
     * @dev Event emitted when authorization is revoked.
     * @param user The address of the user who revoked authorization.
     * @param serviceProvider The address of the service provider.
     */
    event AuthorizationRevoked(address indexed user, address indexed serviceProvider);

    /**
     * @dev Registers a new service provider.
     * @param serviceProvider The address of the service provider to be registered.
     */
    function registerServiceProvider(address serviceProvider) external;

    /**
     * @dev Deregisters a service provider.
     * @param serviceProvider The address of the service provider to be deregistered.
     */
    function deregisterServiceProvider(address serviceProvider) external;

    /**
     * @dev Allows users to authorize a service provider to deduct funds.
     * @param serviceProvider The address of the service provider to authorize.
     * @param amount The amount of funds to authorize for deduction.
     */
    function authorizeServiceProvider(address serviceProvider, uint256 amount) external;

    /**
     * @dev Revokes authorization for a service provider to deduct funds.
     * @param serviceProvider The address of the service provider for which authorization is to be revoked.
     */
    function revokeAuthorization(address serviceProvider) external;

    /**
     * @dev Deducts funds from the user's pre-authorized amount.
     * @param user The address of the user from whom funds will be deducted.
     * @param amount The amount of funds to deduct.
     * @param _data The data associated with the deduction.
     */
    function deductAuthorizedFunds(address user, uint256 amount, bytes calldata _data) external;

    /**
     * @dev Returns the authorized amount for a user and service provider.
     * @param user The address of the user.
     * @param serviceProvider The address of the service provider.
     * @return The authorized amount.
     */
    function authorizedAmount(address user, address serviceProvider) external view returns (uint256);

    /**
     * @dev Checks if a service provider is registered.
     * @param serviceProvider The address of the service provider to check.
     * @return True if the service provider is registered, otherwise false.
     */
    function isServiceProviderRegistered(address serviceProvider) external view returns (bool);
}
```

## Rationale

The choice of using [ERC-20](./eip-20.md) tokens for payments was driven by their widespread adoption. The benefits of public centralized management, including the utilization of service providers and fund pools, are twofold. Firstly, it enhances transparency and accountability by enabling clear oversight of the entities involved in the protocol. Secondly, it facilitates efficient resource allocation and governance, enabling effective decision-making processes and ensuring the integrity of the ecosystem.

## Reference Implementation

```solidity
pragma solidity ^0.8.20;

import "@openzeppelin/contracts/token/ERC20/IERC20.sol";
import "@openzeppelin/contracts/utils/math/SafeMath.sol";
import "@openzeppelin/contracts/security/ReentrancyGuard.sol";

contract ERC7622 is ReentrancyGuard {
    using SafeMath for uint256;
    IERC20 public payToken; // ERC20 token used for payments
    mapping(address => mapping(address => uint256)) private _authorizedAmounts; // User -> (Service Provider -> Authorized Amount)
    mapping(address => bool) public registeredServiceProviders; // Registered service providers

    // Event definitions
    event ServiceProviderRegistered(address serviceProvider);
    event ServiceProviderDeregistered(address serviceProvider);
    event AuthorizationUpdated(address indexed user, address indexed serviceProvider, uint256 newAuthorizedAmount);
    event FundsDeducted(address indexed user, address indexed serviceProvider, uint256 amount, bytes _data);
    event AuthorizationRevoked(address indexed user, address indexed serviceProvider);

    constructor(address payTokenAddress) {
        require(payTokenAddress != address(0), "Invalid token address");
        payToken = IERC20(payTokenAddress);
    }

    // Register a new service provider
    function registerServiceProvider(address serviceProvider) external {
        require(!registeredServiceProviders[serviceProvider], "Service provider already registered");
        registeredServiceProviders[serviceProvider] = true;
        emit ServiceProviderRegistered(serviceProvider);
    }

    // Deregister a service provider
    function deregisterServiceProvider(address serviceProvider) external {
        require(registeredServiceProviders[serviceProvider], "Service provider not registered");
        registeredServiceProviders[serviceProvider] = false;
        emit ServiceProviderDeregistered(serviceProvider);
    }

    // Allows users to pre-authorize a service provider to deduct funds up to a certain limit
    function authorizeServiceProvider(address serviceProvider, uint256 amount) external nonReentrant {
        require(amount > 0, "Invalid authorize amount");
        require(registeredServiceProviders[serviceProvider], "Service provider not registered");
        uint256 authorized_amount = _authorizedAmounts[msg.sender][serviceProvider];
        
        if (authorized_amount == 0) {
            require(payToken.transferFrom(msg.sender, address(this), amount), "Failed to transfer funds");
        } else {
            require(authorized_amount != amount, "Authorized amount unchanged");
            if (amount < authorized_amount) {
                uint256 returnAmount = authorized_amount.sub(amount);
                require(payToken.transfer(msg.sender, returnAmount), "Failed to transfer funds");
            }
        }
        
        _authorizedAmounts[msg.sender][serviceProvider] = amount;
        emit AuthorizationUpdated(msg.sender, serviceProvider, amount);
    }

    // Revokes authorization for a service provider to deduct funds
    function revokeAuthorization(address serviceProvider) external nonReentrant {
        uint256 authorized_amount = _authorizedAmounts[msg.sender][serviceProvider];
        require(authorized_amount > 0, "No authorization found");
        _authorizedAmounts[msg.sender][serviceProvider] = 0;
        require(payToken.transfer(msg.sender, authorized_amount), "Failed to transfer funds");
        emit AuthorizationRevoked(msg.sender, serviceProvider);
    }

    // Deducts funds from the user's pre-authorized amount
    function deductAuthorizedFunds(address user, uint256 amount, bytes calldata _data) external nonReentrant {
        require(registeredServiceProviders[msg.sender], "Only registered service providers can deduct funds");
        require(_authorizedAmounts[user][msg.sender] >= amount, "Insufficient authorized amount");
        _authorizedAmounts[user][msg.sender] = _authorizedAmounts[user][msg.sender].sub(amount);
        require(payToken.transfer(msg.sender, amount), "Failed to transfer funds");
        emit FundsDeducted(user, msg.sender, amount, _data);
    }

    // View authorized amount
    function authorizedAmount(address user, address serviceProvider) external view returns (uint256) {
        return _authorizedAmounts[user][serviceProvider];
    }

    // Check if a service provider is registered
    function isServiceProviderRegistered(address serviceProvider) external view returns (bool) {
        return registeredServiceProviders[serviceProvider];
    }
}
```

## Security Considerations

The protocol includes several security measures to ensure the safety of user funds and the integrity of service provider transactions. These measures include checks for service provider registration, user authorization levels, and transaction limits. Implementations of this protocol must rigorously enforce these security checks to prevent unauthorized fund deductions and protect against potential abuses.

## Copyright

Copyright and related rights waived via [CC0](../LICENSE.md).

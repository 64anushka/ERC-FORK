---
eip: 7583
title: Inscription In Smart Contract
description: A framework for integrating inscription data within smart contract events.
author: Shalom Mizrahi (@shalom-ins) <insevm@insevm.trade>
discussions-to: https://ethereum-magicians.org/t/discussion-on-erc-7583-for-inscribing-assets-in-smart-contract/17661
status: Draft
type: Standards Track
category: ERC
created: 2023-12-26
---

## Abstract

This EIP proposes a standardized method for enabling tokens to be traded both as fungible tokens and non-fungible tokens on the Ethereum, providing the necessary flexibility for tokens to function in both capacities. Furthermore, this standard incorporates a standard to embed inscription data within event data. This addition allows for the inclusion of immutable, extra information directly on the Ethereum. 

## Motivation

Since March 2023, inscriptions have gradually gained recognition in the market as a new form of asset issuance. The core idea of inscriptions is to embed specific data or information into the blockchain in an immutable form, granting the data characteristics of tamper-proofing and permanent storage. While on the Bitcoin network, inscriptions cannot be directly computed and processed on-chain, the community widely acknowledges the pattern of on-chain storage and off-chain interpretation. This approach ensures the security and authenticity of the data while showcasing the potential of blockchain technology in data ownership.

However, there are many limitations in the existing implementations of inscriptions. The Ethereum Virtual Machine (EVM) serves as a powerful smart contract execution environment capable of handling more complex logic and interactions. Currently, the application of inscriptions is primarily limited to simple data recording, without fully leveraging the computational capabilities of the EVM. This has resulted in the untapped potential of inscriptions, particularly in application scenarios that require on-chain logic processing and interaction with smart contracts.

Therefore, it is crucial to develop an EIP that standardizes the handling of inscription assets. Such a standard would not only facilitate the effective utilization of inscription data but also unlock new application scenarios such as on-chain copyright management, data ownership verification, digital identity authentication, and more. By establishing an inscription asset protocol within the EVM, we can make inscription data an integral part of the EVM's computation capabilities, thereby expanding the scope of inscription usage in decentralized finance (DeFi), non-fungible tokens (NFTs), and other blockchain applications.

## Specification

The key words "MUST", "MUST NOT", "REQUIRED", "SHALL", "SHALL NOT", "SHOULD", "SHOULD NOT", "RECOMMENDED", "NOT RECOMMENDED", "MAY", and "OPTIONAL" in this document are to be interpreted as described in RFC 2119 and RFC 8174.

**Every [ERC-7583](./eip-7583.md) compliant contract MUST implement the ERC-7583 and [ERC-165](./eip-165) interfaces.**

```solidity
pragma solidity ^0.8.0;

/**
 * @title ERC-7583 Inscription Standard in Smart Contracts 
 * 
 * Note: the ERC-165 identifier for this interface is 0x4644c7dc.
 */
interface IERC7583 /* is IERC20, IERC165 */{
    /**
     * @dev Emitted when `value` fungible tokens of inscriptions are moved from one inscription (`from`) to
     * another (`to`).
     *
     * Note that `value` MAY be zero.
     */
    event TransferInsToIns(uint256 indexed fromIns, uint256 indexed toIns, uint256 value);

    /**
     * @dev Emitted when `inscriptionId` inscription is transferred from `from` to `to`.
     */
    event TransferIns(address indexed from, address indexed to, uint256 indexed inscriptionId);

    /**
     * @dev Emitted when `owner` enables `approved` to manage the `insId` inscription.
     */
    event ApprovalIns(address indexed owner, address indexed approved, uint256 indexed insId);

    /**
     * @dev Emitted when `owner` enables or disables (`approved`) `operator` to manage all of its inscriptions.
     */
    event ApprovalForAll(address indexed owner, address indexed operator, bool approved);

    /**
     * @dev Returns the amount of inscriptions owned by `account`.
     */
    function insBalance(address account) external view returns (uint256);

    /**
     * @dev Returns the value of fungible tokens in the inscription(`indId`).
     */
    function balanceOfIns(uint256 insId) external view returns (uint256);

    /**
     * @dev Returns the owner of the `insId` inscription.
     *
     * Requirements:
     *
     * - `insId` MUST exist.
     */
    function ownerOf(uint256 insId) external view returns (address owner);

    /**
     * @dev Gives permission to `to` to transfer `insId` inscription to another account.
     * The approval is cleared when the inscription is transferred.
     *
     * Only a single account can be approved at a time, so approving the zero address clears previous approvals.
     *
     * Requirements:
     *
     * - The caller MUST own the inscription or be an approved operator.
     * - `insId` MUST exist.
     *
     * Emits an {ApprovalIns} event.
     */
    function approveIns(address to, uint256 insId) external returns (bool);

    /**
     * @dev Approve or remove `operator` as an operator for the caller.
     * Operators can call {transferInsFrom} or {safeTransferFrom} for any inscription owned by the caller.
     *
     * Requirements:
     *
     * - The `operator` MUST NOT the address zero.
     *
     * Emits an {ApprovalForAll} event.
     */
    function setApprovalForAll(address operator, bool approved) external;

    /**
     * @dev Returns the account approved for `insId` inscription.
     *
     * Requirements:
     *
     * - `insId` MUST exist.
     */
    function getApproved(uint256 insId) external view returns (address operator);

    /**
     * @dev Returns if the `operator` is allowed to manage all of the inscriptions of `owner`.
     *
     * See {setApprovalForAll}
     */
    function isApprovedForAll(address owner, address operator) external view returns (bool);

    /**
     * @dev Transfers `insId` inscription from `from` to `to`.
     *
     * WARNING: Note that the caller MUST confirm that the recipient is capable of receiving inscription
     * or else they MAY be permanently lost. Usage of {safeTransferFrom} SHOULD prevents loss, though the caller MUST
     * understand this adds an external call which potentially creates a reentrancy vulnerability.
     *
     * Requirements:
     *
     * - `from` MUST NOT the zero address.
     * - `to` MUST NOT the zero address.
     * - `insId` inscription MUST be owned by `from`.
     * - If the caller is not `from`, it MUST be approved to move this inscription by either {approveIns} or {setApprovalForAll}.
     *
     * Emits a {TransferIns} event.
     */
    function transferInsFrom(address from, address to, uint256 insId) external;

    /**
     * @dev Transfers `amount` FTs from inscription `from` to address `to`.
     *
     * Requirements:
     *
     * - msg.sender MUST be the owner of inscription `from`.
     * - `to` cannot be the zero address.
     *
     * Returns a boolean value indicating whether the operation succeeded.
     *
     * Emits a {TransferInsToIns} event.
     */
    function transferFTIns(uint256 from, address to, uint256 amount) external returns (bool);

    /**
     * @dev Moves a `value` amount of FTs from inscription `from` to address `to`
     *
     * Requirements:
     *
     * - If the caller is not the owner of inscription `from`, it must have been allowed to move this inscription by either {approveIns} or {setApprovalForAll}.
     * - `to` cannot be the zero address.
     *
     * Emits a {TransferInsToIns} event.
     */
    function transferFTInsFrom(uint256 from, address to, uint256 amount) external returns (bool);

    /**
     * @dev Safely transfers `insId` inscription from `from` to `to`, checking first that contract recipients
     * are aware of the ERC-7583 protocol to prevent tokens from being forever locked.
     *
     * Requirements:
     *
     * - `from` MUST NOT the zero address.
     * - `to` MUST NOT the zero address.
     * - `insId` token MUST exist and be owned by `from`.
     * - If the caller is not `from`, it MUST have been allowed to move this token by either {approveIns} or {setApprovalForAll}.
     * - If `to` refers to a smart contract, it MUST implement {IERC7583Receiver-onERC7583Received}, which is called upon
     *   a safe transfer.
     *
     * Emits a {TransferIns} event.
     */
    function safeTransferFrom(address from, address to, uint256 insId) external;
}
```

A wallet/broker/auction application MUST implement the wallet interface if it will accept safe transfers.

```solidity
pragma solidity ^0.8.0;
/**
 * @title ERC-7583 token receiver interface 
 * 
 * Note: the ERC-165 identifier for this interface is 0xee1c3373.
 */
interface IERC7583Receiver {
    /**
    * @notice Handle the receipt of an inscription
    * @dev The ERC-7583 smart contract calls this function on the recipient
    *  after a `transfer`. This function MAY throw to revert and reject the
    *  transfer. Return of other than the magic value MUST result in the
    *  transaction being reverted.
    *  Note: the contract address is always the message sender.
    *  - _operator The address which called `safeTransferFrom` function
    *  - _from The address which previously owned the token
    *  - _tokenId The insctiption identifier which is being transferred
    * @dev return `bytes4(keccak256("onERC7583Received(address,address,uint256)"))`
    *  unless throwing
    */
    function onERC7583Received(address _operator, address _from, uint256 _tokenId) external returns(bytes4);
}
```

The metadata extension is OPTIONAL for ERC-7583 smart contracts. This allows your smart contract to be interrogated for its name, symbol, decimals and for details about the assets which your inscriptions represent.

```solidity
pragma solidity ^0.8.0;

/**
 * @dev Interface for the OPTIONAL metadata functions from the ERC-7583 standard.
 *  Note: the ERC-165 identifier for this interface is 0xe45d67f9.
 */
interface IERC7583Metadata /* is IERC7583 */ {
    /**
     * @dev Embed the inscription data corresponding to the insId into Ethereum through events.
     */
    event Inscribe(uint256 indexed id, bytes data);
    
    /**
     * @dev Returns the name of the inscription token.
     */
    function name() external view returns (string memory);

    /**
     * @dev Returns the symbol of the inscription token.
     */
    function symbol() external view returns (string memory);

    /**
     * @dev Returns the decimals places of the inscription token.
     */
    function decimals() external view returns (uint8);
}
```

## Rationale

The BRC20 protocol is a standard within the Bitcoin ecosystem that introduces a new way of transacting Fungible Tokens (FTs), allowing these tokens to be traded in a manner similar to Non-Fungible Tokens (NFTs). Specifically, the BRC20 protocol enables a number of FTs to be bundled together and traded as a single entity, offering more flexibility and possibilities for fungible token transactions. Within the Bitcoin ecosystem, the BRC20 protocol is primarily executed through off-chain interpreters. This approach relies on external participants and infrastructure to recognize and process these special transactions.

In the Ethereum Virtual Machine (EVM), smart contracts provide powerful computational capabilities and a flexible programming model, making it feasible to implement the transaction modes described by the BRC20 protocol on Ethereum.

### Inheriting [ERC-20](./eip-20)

The ERC-7583 standard aims to combine the advantages of fungible tokens with new inscription functionalities, providing developers and users with a token standard that is both familiar and innovative. ERC-20, as the most widely used token standard on Ethereum, offers a clear and efficient set of rules for the issuance and trading of fungible tokens (FTs). Additionally:

- **Broad Ecosystem Support**: ERC-20 tokens, due to their simplicity and high degree of compatibility, have gained broad ecosystem support, including wallets, exchanges, DeFi platforms, etc. By inheriting ERC-20, tokens under the ERC-7583 standard can seamlessly integrate into this mature ecosystem, thereby leveraging existing infrastructure and tools.

- **User Familiarity**: The ERC-20 standard, due to its long-standing presence and popularity within the Ethereum community, is well-known among users. Users have a clear understanding of how to trade and manage ERC-20 tokens, which reduces the educational and adaptation costs when promoting a new standard.

### Design Decisions for the NFT Module

In the design process of the ERC-7583 standard, we thoroughly considered how to effectively integrate the characteristics of non-fungible tokens (NFTs), which was achieved by drawing on the [ERC-721](./eip-721.md) standard. ERC-721 provides a widely accepted set of interface definitions and transaction specifications for NFTs, making it the representative standard for unique assets on Ethereum. However, in attempting to integrate features from ERC-721 into ERC-7583, we encountered several key challenges and constraints:

- **Naming conflicts for functions and events**: There are multiple functions and events with similar functionalities but different purposes between ERC-20 and ERC-721. These duplicated interfaces, if used directly in the same contract, could lead to confusion and compatibility issues. For example, the `transfer` and `approve` functions exist in both standards but deal with different types of assets and logic.

Therefore, we made the following design decisions:

- **Modifying function names**: To preserve the full capabilities of ERC-721 while avoiding naming conflicts, we modified the names of functions and events borrowed from ERC-721. This approach maintains clarity and consistency of the interface while avoiding direct conflicts with ERC-20 features.

- **Preserving core features of ERC-721**: Through this method, we ensured that the ERC-7583 standard could support the core features of NFTs, such as unique identification, ownership management, and secure transfer.

#### Indirect Holding and Management of Fungible Tokens (FT) Through Inscriptions

The ERC-7583 standard introduces a novel mode of token holding and trading, namely, the indirect holding and management of Fungible Tokens (FT) through inscriptions. The core of this mode lies in the `balanceOfIns` function and the `TransferInsToIns` event, which allow for querying the amount of FT stored in a specific inscription ID and tracking the transfer path of fungible tokens. The motivations and considerations behind this design are as follows:

- **Enhanced Ownership Expression**: In the traditional ERC-20 standard, the ownership of tokens is directly associated with a user's address. In ERC-7583, by introducing the concept of inscriptions, we associate the ownership of FT with inscription IDs. Thus, users indirectly hold the corresponding FT by possessing specific inscriptions.

- **Flexible Token Management**: By carrying FT through inscriptions, the ERC-7583 standard supports more flexible token management and transaction methods. For example, users can easily transfer or trade a group of FT as a whole without having to deal with each token individually. This transaction method can effectively avoid the price impact of large transactions, opening new possibilities for token application scenarios.

- **Increased Data Richness**: Associating FT with inscription IDs also allows for the embedding of additional data and information in inscriptions through `event Inscribe(uint256 indexed id, bytes data);`. This adds a layer of data richness to the tokens, meaning that each inscription carrying FT is not just about quantity information but can also include multidimensional data such as copyrights, authentication, and usage rights.

- **Improved Security and Transparency**: By using inscriptions as the medium for carrying and trading FT, it is possible to clearly track the token's transfer path and ownership change history on the blockchain, thereby enhancing the security and transparency of transactions.

## Backwards Compatibility

### FT Section

#### Compatibility and Challenges

The ERC-7583 standard fully inherits the ERC-20 interface, meaning it is compatible with all existing DeFi protocols. This design decision ensures that ERC-7583 standard tokens can be seamlessly integrated into the existing Ethereum ecosystem, including decentralized exchanges, lending platforms, and other DeFi products.

However, since the FT in the ERC-7583 standard is carried through inscriptions, this introduces a specific challenge: it is not directly possible within the contract to iterate over all inscriptions held by users to calculate their total FT balance. The balanceOf function cannot directly return the total FT balance a user owns, as FT might be dispersed across multiple inscriptions held by the user.

#### Solution

To address this challenge, we introduced a special inscription as the user's FT storage slot. This special inscription acts as a medium for users to participate in DeFi protocols, allowing them to interact with various DeFi protocols through it. Specifically, we provide the following mechanisms and functions:

- **FT Slot**: Each user can have one special inscription, serving as the primary storage location for their FT. When participating in existing DeFi protocols, only the FT amount in the FT Slot is decreased or increased, leaving FT in other inscriptions unaffected.

- **Deposit Function**: Users can transfer their FT from other inscriptions into this FT Slot. This process is facilitated by calling a specific function that allows transferring FT out of a specified inscription and depositing it into the FT Slot.

- **Withdrawal Function**: Similarly, users can also transfer FT out of their FT Slot to any other inscription. This facilitates flexible management and use of FT.

Through this design, although it is not directly possible to iterate over all inscriptions to calculate the total FT balance in the contract, users can manage their FT assets by controlling the FT in their inscriptions. This not only resolves the limitations of the balanceOf function but also provides users with a more flexible and efficient way to manage their FT.

### NFT Section

#### Compatibility Retention

To integrate NFT functionalities within the ERC-7583 standard, we referred to the core features of the ERC-721 standard and made necessary adjustments. Despite the need to modify some event and function names due to naming conflicts, we endeavored to minimize changes to the core functionalities of ERC-721. In the ERC-7583 standard, we retained the following core components of ERC-721 to ensure compatibility with the existing NFT ecosystem:

- **Events**: `ApprovalForAll`
- **Functions**: `ownerOf`, `safeTransferFrom`, `setApprovalForAll`, `getApproved`, `isApprovedForAll`

#### Incompatibilities and Their Impacts

Despite our efforts to maintain compatibility, the introduction of the ERC-7583 standard still presents some incompatibility issues with the existing NFT market:

- **Transfer Function Differences**: Existing NFT markets that use `transferFrom` to transfer NFTs will not be able to directly support the ERC-7583 standard. This is because the corresponding functionality in ERC-7583 is `transferInsFrom`, leading to direct calling incompatibilities.


- **Changes in FT Quantity Within Inscriptions**: In the ERC-7583 standard, inscriptions can carry a variable number of FTs. This feature requires NFT markets to be able to track the latest changes in the quantity of FTs within inscriptions, otherwise, users might purchase inscriptions that do not contain the expected FTs due to being unaware of the current status.

- **Quoting Mechanism Changes**: Since the quantity of FTs within inscriptions is variable, the traditional fixed-attribute quoting mechanism in NFT markets may no longer be applicable. Markets and users need new mechanisms to handle this dynamic.

#### Solutions and Market Adaptations

Currently, we have not found a perfect solution to overcome the above-mentioned incompatibility issues without requiring adjustments from existing NFT markets. Therefore, to be compatible with the ERC-7583 standard, NFT markets may need to make the following changes:

- **Update Contract Interaction Logic**: NFT markets need to update their contract interaction logic to support the modified functions in the ERC-7583 standard, especially those related to NFT transfers.
- **Implement Inscription Tracking Mechanisms**: To ensure that users and markets can accurately understand the current state of FTs within inscriptions, markets need to implement effective tracking and updating mechanisms to monitor changes in the quantity of FTs within inscriptions.
- **Adjust Quoting and Trading Mechanisms**: Considering the variability in the quantity of FTs within inscriptions, NFT markets may need to develop new quoting and trading mechanisms to accommodate this new dynamic.

## Reference Implementation

- [ERC-7583 implementation](../assets/eip-7583/contracts/ERC7583.sol)

## Security Considerations

### Inscription Management and Authorization Security

In the ERC-7583 standard, the concept of inscriptions is introduced, allowing users to indirectly manage the fungible tokens (FT) contained within by managing inscriptions. This design increases the flexibility of token management but also introduces new security considerations. It could lead to users over-authorizing third parties (operators) without fully understanding the authorization mechanism, thereby increasing the risk of assets being mishandled or maliciously manipulated.

The `setApprovalForAll` function authorizes an operator for all of a user's current and future inscriptions, effectively granting the operator permission to manage all current and future FTs of the specified user.

On the other hand, the quantity of fungible tokens (FT) contained within an inscription is variable. When users use the approveIns function to authorize a third party to operate a specific inscription, they also authorize the FT contained within it. Changes in the quantity of FT within that inscription later on may lead to the actual number of FT authorized not aligning with the user's original intention. This mechanism could be exploited, leading to users inadvertently authorizing more FT to a third party than intended.

### Data Integrity and Validity

The Inscribe event allows for the embedding of arbitrary data, which raises concerns about the integrity and validity of the data being inscribed. It is essential to consider mechanisms to verify the authenticity and accuracy of the data, especially when used in applications that rely heavily on the integrity of inscription data.

Implementations should consider using cryptographic techniques like digital signatures or Merkle tree to validate the data before inscribing it on the blockchain.

### Privacy Concerns

Depending on the nature of the inscribed data, there may be privacy implications, especially if personally identifiable information (PII) or sensitive data is being stored on-chain.

Implementations should provide clear guidelines on the types of data suitable for inscription and consider incorporating privacy-preserving techniques where necessary.

## Copyright

Copyright and related rights waived via [CC0](../LICENSE.md).
